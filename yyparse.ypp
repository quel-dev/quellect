%{
#ifndef NODE_H_
#define NODE_H_
#include "node.h"
#endif

#ifndef YYLEX_HPP_
#define YYLEX_HPP_
#include "yylex.hpp"
#endif
 
#include <cstdio>

void yyerror(Node* root, const char* msg) {
  fprintf(stderr, "%s\n", msg);
}

%}

%union {
  Node* node;
  IntToken* int_token;
  DoubleToken* double_token;
  StringToken* token;
  TypePattern* r_type_node;
}

%parse-param { Node*& root }

%start start

%token<token> EQ
%token<token> LT
%token<token> GT
%token<token> LE
%token<token> GE
%token<token> LOGOR
%token<token> LOGAND
%token<token> LOGNOT
%token<token> ALIAS
%token<token> TYPE
%token<token> INT
%token<token> DOUBLE
%token<token> STRING
%token<token> NUL
%token<token> FUNC
%token<token> FUNC_BRAC
%token<token> IF
%token<token> ELSE
%token<token> SWITCH
%token<token> EVENT
%token<token> BEHAV
%token<token> RETURN
%token<token> BREAK
%token<token> CONTINUE
%token<token> IDEN
%token<int_token> INT_NUM
%token<double_token> DOUBLE_NUM
%token<token> CONST_STR

%type<node> start
%type<node> assignment-expression
%type<node> constant-expression
%type<node> logical-or-expression
%type<node> logical-and-expression
%type<node> equal-expression
%type<node> notequal-expression
%type<node> notequal-operator
%type<node> additive-expression
%type<node> multiplicative-expression
%type<node> unary-expression
%type<node> postfix-expression
%type<node> primary-expression
%type<node> primary-type
%type<node> newtype-definition
%type<node> type
%type<node> type-comma-list
%type<node> declaration
%type<node> expression
%type<node> expression-comma-list
%type<node> statement
%type<node> compound-statement
%type<node> selection-statement
%type<node> switch-branch
%type<node> behavior-statement
%type<node> event-statement
%type<node> jump-statement
%type<node> function-expression
%type<r_type_node> parameter-pair-comma-list
%type<r_type_node> r-type-definition
%type<r_type_node> r-type-definition-list


%%
start
  : compound-statement                                  { root = $1; }
  ;

assignment-expression 
  : expression ';'                                      { $$ = $1;}
  | expression '=' assignment-expression                {
                                                          $$ = new BinaryOpExpNode($1, $3, "=");
                                                        }
  ;

constant-expression
  : logical-or-expression                               {
                                                          $$ = $1;
                                                        }
  ;

logical-or-expression
  : logical-and-expression                              {
                                                          $$ = $1;
                                                        }
  | logical-or-expression LOGOR logical-and-expression  {
                                                          $$ = new BinaryOpExpNode($1, $3, "||");
                                                          delete($2);
                                                        }
  ;

logical-and-expression
  : equal-expression                                    {
                                                          $$ = $1;
                                                        }
  | logical-and-expression LOGAND equal-expression      {
                                                          $$ = new BinaryOpExpNode($1, $3, "&&");
                                                          delete($2);
                                                        }
  ;

equal-expression
  : notequal-expression                                 {
                                                          $$ = $1;
                                                        }
  | equal-expression EQ notequal-expression             {
                                                          $$ = new BinaryOpExpNode($1, $3, "==");
                                                          delete($2);
                                                        }
  ;

notequal-expression
  : additive-expression                                 {
                                                          $$ = $1;
                                                        }
  | additive-expression notequal-operator additive-expression  {
                                                                 $$ = new BinaryOpExpNode($1, $3, "!=");
                                                                 delete($2);
                                                               }
  ;
                       
notequal-operator
  : LT                                                         { $$ = $1;}
  | GT                                                         { $$ = $1;}
  | LE                                                         { $$ = $1;}
  | GE                                                         { $$ = $1;}
  ;

additive-expression
  : multiplicative-expression                                 {
                                                                $$ = $1;
                                                              }
  | additive-expression '+' multiplicative-expression         {
                                                                $$ = new BinaryOpExpNode($1, $3, "+");
                                                              }
  | additive-expression '-' multiplicative-expression         {
                                                                $$ = new BinaryOpExpNode($1, $3, "-");
                                                              }
  ;

multiplicative-expression
  : unary-expression                                          {
                                                                $$ = $1;
                                                              }
  | multiplicative-expression '*' unary-expression            {
                                                                $$ = new BinaryOpExpNode($1, $3, "*");
                                                              }
  | multiplicative-expression '/' unary-expression            {
                                                                $$ = new BinaryOpExpNode($1, $3, "/");
                                                              }
  | multiplicative-expression '%' unary-expression            {
                                                                $$ = new BinaryOpExpNode($1, $3, "%");
                                                              }
  ;

unary-expression
  : postfix-expression                                        {
                                                                $$ = $1;
                                                              }
  | '!' postfix-expression                                    {
                                                                $$ = new UnaryOpExpNode($2, "!");
                                                              }
  ;

postfix-expression
  : primary-expression                                        {
                                                                $$ = $1;
                                                              }
  | postfix-expression '[' constant-expression ']'            {
                                                                $$ = new BinaryOpExpNode($1, $3, "[]");
                                                              }
  ;

primary-expression
  : IDEN                                                      { $$ = $1;}
  | INT_NUM                                                   { $$ = $1;}
  | DOUBLE_NUM                                                { $$ = $1;}
  | CONST_STR                                                 { $$ = $1;}
  | '(' constant-expression ')'                               {
                                                                $$ = $2;
                                                              }
  | function-expression                                       {
                                                                $$ = $1;
                                                              }
  ;

primary-type
  : INT                                                       { $$ = $1; }
  | DOUBLE                                                    { $$ = $1; }
  | STRING                                                    { $$ = $1; }
  | NUL                                                       { $$ = $1; }
  | FUNC                                                      { $$ = $1; }
  ;

newtype-definition
  : ALIAS IDEN '=' type                                       {
                                                                $$ = new TypeDefNode(($2)->GetStrTok(), $4);
                                                                delete($1);
                                                              }
  | TYPE IDEN '=' IDEN FUNC_BRAC type-comma-list GT           {
                                                                Node* cons = new ConsDefNode(($4)->GetStrTok(), $6);
                                                                $$ = new TypeDefNode(($2)->GetStrTok(), cons);
                                                                delete($1);  // token 'type'
                                                                delete($5);  // token FUNC_BRAC
                                                              }
  | newtype-definition '|' IDEN FUNC_BRAC type-comma-list GT  {
                                                                $$ = $1;
                                                                Node* cons = new ConsDefNode(($3)->GetStrTok(), $5);
                                                                ($$)->Add(cons);
                                                                delete($4);  // token FUNC_BRAC
                                                              }
  ;

type
  : primary-type                                              { $$ = $1;}
  | IDEN                                                      { $$ = $1;}
  ;

type-comma-list
  : type                                                      { $$ = new ListNode("type_list", $1); }
  | type-comma-list ',' type                                  {
                                                                $$ = $1;
                                                                ($$)->Add($3);
                                                              }
  ;

declaration
  : newtype-definition ';'                                                  {
                                                                              $$ = $1;
                                                                            }
  | FUNC IDEN '(' parameter-pair-comma-list ')' '{' compound-statement '}'  {
                                                                              $$ = new FuncDef(($2)->GetStrTok(), $4, $7);
                                                                            }
  ;

parameter-pair-comma-list
  : r-type-definition                                                   {
                                                                          $$ = new TypePattern(""); ($$)->Add($1);
                                                                        }
  | r-type-definition ',' parameter-pair-comma-list                     {
                                                                          $$ = $3;
                                                                          ($$)->Add($1);
                                                                        }
  ;

r-type-definition
  : IDEN                                                                { $$ = new TypePattern(($1)->GetStrTok()); }

  | IDEN FUNC_BRAC r-type-definition-list  GT                                { ($$) = ($3); ($$)->SetConsName(($1)->GetStrTok()); }

  ;

r-type-definition-list
  : r-type-definition                                                   {$$ = new TypePattern(""); ($$)->Add($1);}

  | r-type-definition ',' r-type-definition-list                        {($3)->Add($1); ($$) = ($3);}

  ;

expression
  : constant-expression                                                 { $$ = $1;}
  ;

expression-comma-list
  : expression                                                          { $$ = new ListNode("exp_list", $1);}

  | expression-comma-list ',' expression                                {
                                                                          $$ = $1;
                                                                          ($$)->Add($3);
                                                                        }
  ;

statement
  : assignment-expression                                               { $$ = $1;}
  | selection-statement                                                 { $$ = $1;}
  | behavior-statement    ';'                                           { $$ = $1;}
  | event-statement                                                     { $$ = $1;}
  | jump-statement        ';'                                           { $$ = $1;}
  ;

compound-statement
  : /* empty */                                                         { $$ = new ListNode("statement_list");}
  | compound-statement statement                                        {
                                                                          $$ = $1;
                                                                          ($$)->Add($2); 
                                                                        }
  | compound-statement declaration                                      {
                                                                          $$ = $1;
                                                                          ($$)->Add($2);
                                                                        }
  ;

function-expression
  : '(' IDEN expression-comma-list ')'                                  { 
                                                                          $$ = new FuncExp(($2)->GetStrTok(), $3);
                                                                        }
  ;

selection-statement
  : IF constant-expression '{' compound-statement '}'                   {
                                                                          $$ = new IfNode($2, $4, NULL);
                                                                          delete($1);
                                                                        }
  | IF constant-expression '{' compound-statement '}'
    ELSE '{' compound-statement '}'                                     {
                                                                          $$ = new IfNode($2, $4, $8);
                                                                          delete($1);
                                                                          delete($6);
                                                                        }
  | SWITCH constant-expression '{' switch-branch '}'                    {
                                                                          $$ = new SwitchNode($2, $4);
                                                                        }
  ;

switch-branch
  : /* empty */                                                         { $$ = new SwitchBranch();}
  | switch-branch constant-expression ':' '{' compound-statement '}'    {
                                                                          $$ = $1;
                                                                          ($$)->AddBranch($2, $5);
                                                                        }
  ;

jump-statement
  : CONTINUE                                                            {
                                                                          $$ = new JumpNode("continue", NULL);
                                                                          delete($1);
                                                                        }
  | BREAK                                                               {
                                                                          $$ = new JumpNode("break", NULL);
                                                                          delete($1);
                                                                        }
  | RETURN constant-expression                                          {
                                                                          $$ = new JumpNode("return", $2);
                                                                          delete($1);
                                                                        }
  | RETURN                                                              {
                                                                          $$ = new JumpNode("return", NULL);
                                                                          delete($1);
                                                                        }
  ;

behavior-statement
  : IDEN BEHAV constant-expression                                      {
                                                                          $$ = new BehaviorNode(($1)->GetStrTok(), $3);
                                                                          delete($2);
                                                                        }
  ;

event-statement
  : constant-expression EVENT function-expression                       {
                                                                          $$ = new EventNode($1, $3); 
                                                                          delete($2);
                                                                        }
  | constant-expression EVENT '{' compound-statement '}'                {
                                                                          $$ = new EventNode($1, $4);
                                                                          delete($2);
                                                                        }
  ;
